plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.8.0'
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.12.0'
}

group 'de.platon42'
version '1.13'
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
}

/*
  To run tests in IntelliJ use these VM Options for run configuration 
  -ea -Didea.system.path=build/idea-sandbox/system-test -Didea.config.path=build/idea-sandbox/config-test -Didea.plugins.path=build/idea-sandbox/plugins-test
 */

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation "org.assertj:assertj-core:3.23.1"
    testImplementation "org.assertj:assertj-guava:3.5.0"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect"
//    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

intellij {
    setVersion("2022.2") // LATEST-EAP-SNAPSHOT
    //pluginName.set(provider { 'Concise AssertJ Optimizing Nitpicker (Cajon)' })
    setUpdateSinceUntilBuild(false)
    setPlugins(["com.intellij.java"])
}

runPluginVerifier {
    ideVersions = ["IC-193.5662.53", "IC-222.3345.118"] // 2020.3 - 2022.2
    downloadDir = System.getProperty("user.home") + "/.gradle/caches/modules-2/files-2.1/com.jetbrains.intellij.idea/verifier"
}

patchPluginXml {
    setChangeNotes("""
  <h4>V1.13 (15-Aug-22)</h4>
    <ul>
      <li>Maintenance. Updated various dependencies (Kotlin 1.7.10) and AssertJ 3.23.1 and AssertJ-Guava 3.5.0.
      <li>Tried to fix unreproducible issue #9.
      <li>Added workaround for upcoming API change in IntelliJ breaking older releases.
      <li>Added AssertThatIsZeroOne inspection demanded by issue #5.
      <li>Fix for wrongly joining statements that cannot be trivially joined (e.g. with filteredOn). Fixes issue #6.
    </ul>
  <p>Full changelog available at <a href="https://github.com/chrisly42/cajon-plugin#changelog">Github project site</a>.</p>
""")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.test {
    systemProperty("idea.force.use.core.classloader", "true")
}

jacoco {
    toolVersion = '0.8.8'
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        csv.required.set(false)
    }
}

publishPlugin {
    setToken(intellijPublishToken)
}