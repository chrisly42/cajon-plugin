plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.10'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
}

group 'de.platon42'
version '1.6'

repositories {
    mavenCentral()
}

/*
  To run tests in IntelliJ use these VM Options for run configuration 
  -ea -Didea.system.path=build/idea-sandbox/system-test -Didea.config.path=build/idea-sandbox/config-test -Didea.plugins.path=build/idea-sandbox/plugins-test
 */

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile "org.assertj:assertj-core:3.13.2"
    testCompile "org.assertj:assertj-guava:3.2.1"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    testImplementation "org.jetbrains.kotlin:kotlin-test"
//    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
intellij {
    version '2019.2.3'
//    pluginName 'Concise AssertJ Optimizing Nitpicker (Cajon)'
    updateSinceUntilBuild false
    plugins = ['java']
}

patchPluginXml {
    changeNotes """
  <h4>V1.6 (unreleased)</h4>
    <ul>
      <li>Really fixed AssertThatGuavaOptional inspections to avoid conversions from .get() to .contains()
          for array types. Sigh. Shouldn't be working &gt;12h a day and then do some more stuff at home.
      <li>Fixed a bug in AssertThatBinaryExpression inspection for assertThat(null != expression) and related
          that would not correctly invert the condition on transformation.
      <li>Added new AssertThatFileExpression to move out many common methods from inside the
          assertThat() expression (exists(), getName(), getParent() and many more).
      <li>Added several transformations to AssertThatStringExpression inspection.
          Specifically, uses of matches(), compareToIgnoreCase(), indexOf(), and trim().
    </ul>
  <p>Full changelog available at <a href="https://github.com/chrisly42/cajon-plugin#changelog">Github project site</a>.</p>
"""
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacoco {
    toolVersion = '0.8.4'
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

publishPlugin {
    token intellijPublishToken
}