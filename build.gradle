plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.26'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.10.2'
}

group 'de.platon42'
version '1.11'
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
}

/*
  To run tests in IntelliJ use these VM Options for run configuration 
  -ea -Didea.system.path=build/idea-sandbox/system-test -Didea.config.path=build/idea-sandbox/config-test -Didea.plugins.path=build/idea-sandbox/plugins-test
 */

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation "org.assertj:assertj-core:3.17.2"
    testImplementation "org.assertj:assertj-guava:3.4.0"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation "org.jetbrains.kotlin:kotlin-test"
//    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

intellij {
    version '2020.2.2' // LATEST-EAP-SNAPSHOT
//    pluginName 'Concise AssertJ Optimizing Nitpicker (Cajon)'
    updateSinceUntilBuild false
    plugins = ['java']
}

patchPluginXml {
    changeNotes """
  <h4>V1.11 (03-Oct-20) Day of German Unity Edition</h4>
    <ul>
      <li>Now is being built with JDK 11 (with Java 8 target).
      <li>Updated various dependencies and AssertJ 3.17.2.
      <li>Fixed the ImplicitAssertionInspection that broke the plugin with IntelliJ 2020.3 EAP as reported by Frédéric Thomas. Thanks!
      <li>Added new singleElement() from AssertJ >= 3.17.0 to ImplicitAssertionInspection.
      <li>Added several cases for hasSizeGreaterThan/LessThan/OrEqualTo() for EnumerablesEmpty inspection.
    </ul>
  <p>Full changelog available at <a href="https://github.com/chrisly42/cajon-plugin#changelog">Github project site</a>.</p>
"""
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacoco {
    toolVersion = '0.8.6'
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

publishPlugin {
    token intellijPublishToken
}