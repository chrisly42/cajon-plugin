plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.1.3'
    id 'org.jetbrains.kotlin.jvm' version '1.5.21'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.11.0'
}

group 'de.platon42'
version '1.12'
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
}

/*
  To run tests in IntelliJ use these VM Options for run configuration 
  -ea -Didea.system.path=build/idea-sandbox/system-test -Didea.config.path=build/idea-sandbox/config-test -Didea.plugins.path=build/idea-sandbox/plugins-test
 */

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation "org.assertj:assertj-core:3.20.2"
    testImplementation "org.assertj:assertj-guava:3.4.0"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.0-M1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.0-M1'
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect"
//    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

intellij {
    setVersion("2021.1.3") // LATEST-EAP-SNAPSHOT
    //pluginName.set(provider { 'Concise AssertJ Optimizing Nitpicker (Cajon)' })
    setUpdateSinceUntilBuild(false)
    setPlugins(["com.intellij.java"])
}

patchPluginXml {
    setChangeNotes("""
  <h4>V1.12 (06-May-21)</h4>
    <ul>
      <li>Maintenance. Updated various dependencies (Kotlin 1.50.0) and AssertJ 3.19.0
      <li>Fixed issue#3 reported by hankem where usingRecursiveComparison() was not considered a complex transformation.
    </ul>
  <p>Full changelog available at <a href="https://github.com/chrisly42/cajon-plugin#changelog">Github project site</a>.</p>
""")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        csv.required.set(false)
    }
}

publishPlugin {
    setToken(intellijPublishToken)
}